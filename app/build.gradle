/*
 * Copyright (C) 2018 Emmanuel MacCaull
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.application'

android {
    compileSdkVersion 29
    defaultConfig {
        applicationId "io.github.emaccaull.sweetnothings"
        minSdkVersion 17
        targetSdkVersion 29
        multiDexEnabled true
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "io.github.emaccaull.sweetnothings.TestRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    viewBinding {
        enabled = true
    }

    buildTypes {
        release {
            minifyEnabled = true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        config {
        }
    }
}

// Remove the android-logger from unit tests
configurations.findAll { it.name.contains("test") }.each { conf ->
    conf.exclude group: 'com.noveogroup.android', module: 'android-logger'
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // Support Libraries
    implementation "androidx.appcompat:appcompat:$appcompat_version"
    implementation 'com.google.android.material:material:1.2.0-alpha06'
    implementation "androidx.constraintlayout:constraintlayout:$constraint_layout_version"
    implementation "androidx.vectordrawable:vectordrawable:1.1.0"
    implementation "androidx.multidex:multidex:$multidex_version"

    // Arch Components / Jetpack
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-common-java8:2.2.0'
    implementation "androidx.room:room-runtime:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-rxjava2:$room_version"
    testImplementation "androidx.room:room-testing:$room_version"

    // RxJava
    implementation "io.reactivex.rxjava2:rxandroid:$rxandroid_version"
    implementation "io.reactivex.rxjava2:rxjava:$rxjava_version"
    implementation 'com.jakewharton.rxbinding3:rxbinding-core:3.1.0'

    // Dependency Injection
    api 'com.google.dagger:dagger:2.26'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.26'
    testAnnotationProcessor 'com.google.dagger:dagger-compiler:2.26'

    // Logging
    implementation "org.slf4j:slf4j-api:$slf4j_version"
    runtimeOnly "com.noveogroup.android:android-logger:$slf4j_noveogroup_version"
    testImplementation "org.slf4j:slf4j-simple:$slf4j_version"

    // Utilities
    implementation "com.google.guava:guava:$guava_version"

    // Dependency Management
    implementation "javax.inject:javax.inject:$javax_inject_version"

    // General Testing
    testImplementation "junit:junit:$junit_version"
    testImplementation "org.mockito:mockito-core:$mockito_version"
    testImplementation "org.hamcrest:hamcrest-all:$hamcrest_version"
    testImplementation "nl.jqno.equalsverifier:equalsverifier:$equalsverifier_version"
    testImplementation 'androidx.arch.core:core-testing:2.1.0'

    androidTestImplementation "org.mockito:mockito-android:$mockito_version" // for semi-integration tests
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test:rules:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.2.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
}
