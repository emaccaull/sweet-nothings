/*
 * Copyright (C) 2018 Emmanuel MacCaull
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.diffplug.gradle.spotless'

android {
    compileSdkVersion 29
    defaultConfig {
        applicationId "io.github.emaccaull.sweetnothings"
        minSdkVersion 17
        targetSdkVersion 29
        multiDexEnabled true
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "io.github.emaccaull.sweetnothings.TestRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    buildFeatures {
        viewBinding = true
    }

    buildTypes {
        release {
            minifyEnabled = true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    signingConfigs {
        config {
        }
    }
}

spotless {
    kotlin {
        ktlint()
    }
    kotlinGradle {
        // same as kotlin, but for .gradle.kts files (defaults to '*.gradle.kts')
        target '*.gradle.kts', 'additionalScripts/*.gradle.kts'
        ktlint()
    }
}

// Remove the android-logger from unit tests
configurations.findAll { it.name.contains("test") }.each { conf ->
    conf.exclude group: 'com.noveogroup.android', module: 'android-logger'
}

dependencies {
    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    // Support Libraries
    implementation "androidx.appcompat:appcompat:1.3.0-alpha01"
    implementation 'com.google.android.material:material:1.3.0-alpha01'
    implementation "androidx.constraintlayout:constraintlayout:2.0.0-beta6"
    implementation "androidx.vectordrawable:vectordrawable:1.1.0"
    implementation "androidx.multidex:multidex:2.0.1"

    // Jetpack
    implementation "androidx.core:core-ktx:1.3.2"
    implementation "androidx.fragment:fragment-ktx:1.2.5"
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-common-java8:2.2.0'
    implementation "androidx.room:room-runtime:2.2.5"
    kapt "androidx.room:room-compiler:2.2.5"
    implementation "androidx.room:room-rxjava2:2.2.5"
    testImplementation "androidx.room:room-testing:2.2.5"

    // RxJava
    implementation "io.reactivex.rxjava2:rxandroid:2.1.1"
    implementation "io.reactivex.rxjava2:rxjava:2.2.17"
    implementation 'com.jakewharton.rxbinding3:rxbinding-core:3.1.0'

    // Dependency Injection
    api 'com.google.dagger:dagger:2.29.1'
    kapt 'com.google.dagger:dagger-compiler:2.29.1'
    kaptTest 'com.google.dagger:dagger-compiler:2.29.1'

    // Logging
    implementation "org.slf4j:slf4j-api:1.7.26"
    runtimeOnly "com.noveogroup.android:android-logger:1.3.6"
    testImplementation "org.slf4j:slf4j-simple:1.7.26"

    // Utilities
    implementation "com.google.guava:guava:27.1-android"

    // Dependency Management
    implementation "javax.inject:javax.inject:1"

    // General Testing
    testImplementation "junit:junit:4.13"
    testImplementation "io.mockk:mockk:1.10.0"
    testImplementation "com.google.truth:truth:1.0.1"
    testImplementation 'androidx.arch.core:core-testing:2.1.0'

    // TODO: remove these once tests are converted to Kotlin
    testImplementation "org.mockito:mockito-core:3.3.1"
    testImplementation "org.mockito:mockito-inline:3.3.1"
    testImplementation "org.hamcrest:hamcrest-all:1.3"
    testImplementation "nl.jqno.equalsverifier:equalsverifier:2.5.2"

    androidTestImplementation "org.mockito:mockito-android:2.28.2" // for semi-integration tests
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test:rules:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.2.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
}
